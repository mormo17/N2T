// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    
    Not(in=instruction[15], out=instructionA);
    
    
    Mux16(a=ALUoutput, b=instruction, sel=instructionA, out=registerA);

    
    Or(a=instructionA, b=instruction[5], out=loadRegisterA);
    ARegister(in=registerA, load=loadRegisterA, out[0..15]=Aoutput);

    Mux16(a[0..15]=Aoutput, b=inM[0..15], sel=instruction[12], out=ALUinput);
    
    And(a=instruction[15], b=instruction[4], out=loadRegisterD);
    DRegister(in=ALUoutput, load=loadRegisterD, out=Doutput);

    ALU(x[0..15]=Doutput, y[0..15]=ALUinput, zx=instruction[11], 
        nx=instruction[10], zy=instruction[9], 
        ny=instruction[8], f=instruction[7], 
        no=instruction[6], out=ALUoutput,
        zr=ALUoutputzr, ng=ALUoutputng);

    //outputs
    And(a=instruction[15], b=instruction[3], out=writeM);
    And16(a=ALUoutput, b=true, out=outM);
    And16(a=Aoutput, b=true, out[0..14]=addressM);
    
    Or(a=ALUoutputzr, b=ALUoutputng, out=zrorng);
    Not(in=zrorng, out=positive);

    // determine jump conditions
    And(a=instruction[0], b=positive, out=jgt);
    And(a=instruction[1], b=ALUoutputzr, out=jeq);
    And(a=instruction[2], b=ALUoutputng, out=jlt);

    
    Or(a=jlt, b=jeq, out=j1);
    Or(a=j1, b=jgt, out=j2);
    And(a=j2 , b=instruction[15], out=loadPc);
    Or(a=loadPc, b=reset, out=notinc);
    Not(in=notinc, out=inc);
    PC(in[0..15]=Aoutput, load=loadPc, inc=inc, 
        reset=reset, out[0..14]=pc[0..14]);


}